use Test::More;
use File::Spec;
use File::Temp;
use t::CLI;

#--------------------------------------------------------------------------#
# Get optional test support
#--------------------------------------------------------------------------#

eval "use Test::Differences";
my $HAVE_DIFF = $@ eq '' ? 1 : 0;

sub diff_or_is {
    my ($got, $expected, $label ) = @_;

    if ( $HAVE_DIFF ) {
        eq_or_diff( $got, $expected, $label );
    }
    else {
        is( $got, $expected, $label );
    }
}

#--------------------------------------------------------------------------#
# setup program
#--------------------------------------------------------------------------#

my $script = File::Spec->catfile( "scripts", "wikidoc" );

if ( ! -r $script ) {
    plan skip_all => "because I couldn't find the wikidoc script to test";
}
else {
    plan 'no_plan' # tests => 9;
}

my $wikidoc = t::CLI->new($script);

#--------------------------------------------------------------------------#
# setup input and expected
#--------------------------------------------------------------------------#

my $input_string = <<'INPUT';
=for wikidoc = Version %%VERSION%%

=cut
INPUT

my $no_define = <<'EXPECTED_NO';
=pod

=head1 Version %%VERSION%%

EXPECTED_NO

my $with_define = <<'EXPECTED_WITH';
=pod

=head1 Version 3.14

EXPECTED_WITH

#--------------------------------------------------------------------------#
# setup temporary files
#--------------------------------------------------------------------------#

my $output_file = File::Temp->new();
my $input_file = File::Temp->new();

# File::Temp defaults to binmode so change that on Windows
if ( $^O eq 'MSWin32' ) {
    binmode $output_file, ":crlf";
    binmode $input_file, ":crlf";
}

# init the input file
print $input_file $input_string;
seek $input_file, 0, 0;

#--------------------------------------------------------------------------#
# Start testing
#--------------------------------------------------------------------------#


#--------------------------------------------------------------------------#
# wikidoc file file -- no keyword defined
#--------------------------------------------------------------------------#

$wikidoc->runs_ok( "$input_file", "$output_file" );

# recover output for testing
seek $output_file, 0, 0;
$got =  do { local $/; <$output_file> };
$got =~ s{\A [^\n]+ \n \n}{}xms; # strip "Generated by" line

diff_or_is( $got, $no_define, 
    "wikidoc output without a defined keyword" 
);

#--------------------------------------------------------------------------#
# wikidoc file file -- with VERSION keyword defined (-d)
#--------------------------------------------------------------------------#

$wikidoc->runs_ok( "-d", "VERSION=3.14", "$input_file", "$output_file" );

# recover output for testing
seek $output_file, 0, 0;
$got =  do { local $/; <$output_file> };
$got =~ s{\A [^\n]+ \n \n}{}xms; # strip "Generated by" line

diff_or_is( $got, $with_define, 
    "wikidoc output with a defined keyword" 
);

