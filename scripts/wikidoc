#!/usr/bin/perl
use strict;
use warnings;

use vars qw( $VERSION );
$VERSION = "0.10";

use Getopt::Std;
use Pod::WikiDoc;
use Pod::Usage;

my %opts;
getopts( "chl:V", \%opts );

if ( $opts{h} ) {
    pod2usage( -verbose => 2 );
    exit;
}

if ( $opts{V} ) {
    print "Version: wikidoc $VERSION\n"; 
    exit;
}
    
my ($input_file, $output_file) = @ARGV;

die "wikidoc: Can't use the same file for input and output.\n"
    if $input_file && $output_file && $input_file eq $output_file;

my $args = {
    comment_blocks => $opts{c} ? 1 : 0,
    comment_prefix_length => $opts{l} ? $opts{l} : 3,
};

my $parser = Pod::WikiDoc->new( $args );

if ( $output_file ) {
    print "Extracting Pod from $input_file\n";
}

if ( -t *STDIN && -t *STDOUT ) {
    my $buffer;
    $buffer .= $_ while <STDIN>;
    print $parser->convert( $buffer );
}
else {
    $parser->filter( { input => $input_file, output => $output_file } );
}

__END__

=head1 NAME

wikidoc - convert mixed Pod and wikidoc text to Pod

=head1 SYNOPSIS

    wikidoc MyModule.pm Mymodule.pod
    
=head1 USAGE

    wikidoc [flags] [inputfile] [outputfile]
    
=head1 OPTIONS

=over

=item *

flags:

=over

=item -c

comments: Include wikidoc in comment-blocks

=item -h

help: Show usage information

=item -l

length: Length of comment-block '#' prefix

=item -V

version: Show version number and copyright

=back

=item *

inputfile: A filename that Pod and wikidoc will be extracted from.  If no
filename is given, data will be read from STDIN.

=item *

outputfile: A filename for the generated Pod.  If no filename is given, Pod
will be written to STDOUT.

=back

=head1 DESCRIPTION

C<wikidoc> processes Pod and wikidoc, converting the wikidoc to Pod, and then
writing the combined document back to a file or standard output. 

See documentation for L<Pod::WikiDoc> for details.

=head1 DIAGNOSTICS

None yet -- probably should add some

=head1 DEPENDENCIES

=over

=item *

L<Getopt::Std>

=item *

L<Pod::Usage>

=item *

L<Pod::WikiDoc>

=back

=head1 BUGS

Please report bugs using the CPAN Request Tracker at 
L<http://rt.cpan.org/NoAuth/Bugs.html?Dist=Pod-WikiDoc>

=head1 AUTHOR

David A Golden (DAGOLDEN)

dagolden@cpan.org

L<http://dagolden.com/>

=head1 COPYRIGHT

Copyright (c) 2005 by David A Golden

This program is free software; you can redistribute
it andE<sol>or modify it under the same terms as Perl itself.

The full text of the license can be found in the
LICENSE file included with this module.


